dnl
dnl    Regina - A Normal Surface Theory Calculator
dnl    Configure Script Generator
dnl
dnl    Copyright (c) 2002-2009, Ben Burton
dnl    For further details contact Ben Burton (bab@debian.org).
dnl
dnl    Process this file with autoconf to produce a configure script.
dnl    Autoconf version >= 2.50 is required.
dnl
dnl    This file is free software; you can redistribute it and/or
dnl    modify it under the terms of the GNU General Public License as
dnl    published by the Free Software Foundation; either version 2 of the
dnl    License, or (at your option) any later version.
dnl
dnl    This file is distributed in the hope that it will be useful, but
dnl    WITHOUT ANY WARRANTY; without even the implied warranty of
dnl    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl    General Public License for more details.
dnl
dnl    You should have received a copy of the GNU General Public
dnl    License along with this program; if not, write to the Free
dnl    Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
dnl    MA 02110-1301, USA.
dnl

###########################################################
#
# Initialisation.
#
###########################################################

AC_INIT(Regina, [4.6], [regina-user@lists.sourceforge.net], regina)
AC_PREREQ(2.50)
AC_CONFIG_AUX_DIR(admin)
AC_CONFIG_SRCDIR(engine/triangulation/ntriangulation.cpp)

# Having a correct --prefix is critical.  Make a reasonable guess.
AC_PREFIX_PROGRAM(meinproc)

# Recommended by the KDE folks to avoid ksh/zsh problems.
unset CDPATH

# This will run AC_CANONICAL_{BUILD,HOST,TARGET}, all of which replace
# AC_CANONICAL_SYSTEM, according to the autoconf docs.
AC_CANONICAL_TARGET

# Transform program names.
AC_ARG_PROGRAM

# The KDE folks say that we should not do the automake initialisation
# until this point.
AM_INIT_AUTOMAKE(regina, [4.6])
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(engine/regina-config.h)

# Do we need to install using a non-standard package name?
AC_ARG_VAR(PKGNAME, [the package name to install as (default: regina)])
if test "x" != "x$PKGNAME"; then
  AC_SUBST(PACKAGE, [$PKGNAME])
fi

# Extract major and minor package version numbers.
REGINA_PACKAGE_VERSIONS

###########################################################
#
# Check for C/C++ tools.
#
###########################################################

AC_CHECK_COMPILERS

# Put back the exception handling that KDE takes out, since we need it
# for boost.
CXXFLAGS="`echo "$CXXFLAGS" | sed s/-fno-exceptions//`"

KDE_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --silent"
AM_KDE_WITH_NLS

# Set the default language to C++ for subsequent tests.
AC_LANG(C++)

AC_CHECK_PROGS(_UTIL_FIND, [find], none)
if test "$_UTIL_FIND" = "none"; then
  REGINA_ERR_MISSING([The find utility], [the build system])
fi

AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LN_S

###########################################################
#
# Check for compiler characteristics.
#
###########################################################

# Check for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_CHECK_TYPES(__int64)

# Check for library functions.
AC_FUNC_ERROR_AT_LINE
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([strtol strtoul])

# Other custom checks.
REGINA_CHECK_LONG_LONG
REGINA_CXX_PARTIAL_SPECIALISATION
REGINA_TYPE_HASH_SET
REGINA_VAR_IOS_NOCREATE
REGINA_LD_ADD_STDCALL_ALIAS
AC_CHECK_KDEMAXPATHLEN

###########################################################
#
# Determine which components the user is disabling.
#
###########################################################

AC_ARG_ENABLE(kdeui, AC_HELP_STRING([--disable-kdeui],
    [do not build the Regina KDE user interface]), [
  if test "$enableval" = "no"; then
    REGINA_DO_NOT_COMPILE([KDEUI])
  fi
])
AC_ARG_ENABLE(python, AC_HELP_STRING([--disable-python],
    [do not build the Regina python module]), [
  if test "$enableval" = "no"; then
    REGINA_DO_NOT_COMPILE([PYTHON])
  fi
])
AC_ARG_ENABLE(mpi, AC_HELP_STRING([--disable-mpi],
    [do not build any Regina MPI-enabled utilities]), [
  if test "$enableval" = "no"; then
    REGINA_DO_NOT_COMPILE([MPI])
  fi
])
AC_ARG_ENABLE(docs, AC_HELP_STRING([--disable-docs],
    [do not build the Regina API documentation]), [
  if test "$enableval" = "no"; then
    REGINA_DO_NOT_COMPILE([DOCSENGINE])
  fi
])
AC_ARG_ENABLE(release-mode, AC_HELP_STRING([--enable-release-mode],
    [additional tasks for preparing new source releases]), [
  if test "$enableval" = "yes"; then
    release_mode=true
  else
    release_mode=false
  fi
])
AM_CONDITIONAL(COND_RELEASE_MODE, test "$release_mode" = true)

###########################################################
#
# Check for documentation tools.
#
###########################################################

REGINA_PROG_OPTIONAL(doxygen, DOXYGEN,
  [Doxygen command for C++ source documentation], [DOCSENGINE],
  [calculation engine documentation])

AC_ARG_VAR([DOCBOOK2MAN], [Command for converting SGML to man pages])
AC_CHECK_PROGS([DOCBOOK2MAN], [docbook2man], none)
AM_CONDITIONAL(HAVE_DOCBOOK2MAN, test "$DOCBOOK2MAN" != "none")

###########################################################
#
# Check for mandatory headers and libraries.
#
###########################################################

# Process --with-extra-includes and --with-extra-libs.
# We move these flags into CFLAGS/CXXFLAGS/LDFLAGS so that non-KDE
# material can benefit from them as well.
KDE_CHECK_EXTRA_LIBS
if test -n "$USER_INCLUDES"; then
  CFLAGS="$CFLAGS $USER_INCLUDES"
  CXXFLAGS="$CXXFLAGS $USER_INCLUDES"
  USER_INCLUDES=
fi
if test -n "$USER_LDFLAGS"; then
  LDFLAGS="$LDFLAGS $USER_LDFLAGS"
  USER_LDFLAGS=
fi

REGINA_HEADER_REQUIRED(zlib.h, [the calculation engine])
REGINA_LIB_REQUIRED(z, zlibVersion, [the calculation engine])

# It seems libm is required by libxml2 when doing static linking.
REGINA_LIB_REQUIRED(m, log10, [the calculation engine])

REGINA_HEADER_REQUIRED(gmp.h, [the calculation engine])
REGINA_LIB_REQUIRED(gmp, __gmpz_init, [the calculation engine])

KDE_CHECK_THREADING
if test "$kde_use_threading" != "yes"; then
  REGINA_ERR_MISSING([POSIX pthread support], [the calculation engine])
fi
LDFLAGS="$LDFLAGS $USE_THREADS"

# The libxml2 check seems to need to be done under C, not C++.
AC_LANG_PUSH(C)
AM_PATH_XML2([2.0.0], , [
  REGINA_ERR_MISSING([Library libxml2], [the calculation engine])
])
AC_LANG_POP(C)

###########################################################
#
# Check for optional headers and libraries.
#
###########################################################

REGINA_HEADER_OPTIONAL(popt.h, [UTILS MPI], [command-line utilities])
REGINA_LIB_OPTIONAL(popt, poptGetContext, UTILS_LIBS, [UTILS MPI],
  [command-line utilities])

AM_ICONV

AM_PATH_CPPUNIT([1.8.0], , [
  REGINA_WARN_MISSING([Library cppunit], [the test suite])
  REGINA_DO_NOT_COMPILE(TESTSUITE)
])

REGINA_SHOULD_BUILD_INTERNAL(PYTHON)
if test "$should_build" = "yes"; then
  REGINA_LIB_BOOST_PYTHON(PYTHON, [the Python interface])
  REGINA_CHECK_BOOST_MAKE_CONSTRUCTOR
fi

REGINA_SHOULD_BUILD_INTERNAL(KDEUI)
if test "$should_build" = "yes"; then
  KDE_USE_QT(3.2, [>= Qt 3.2], [QT_VERSION >= 0x030200])
  KDE_SET_PREFIX
  AC_PATH_KDE
  REGINA_CHECK_KDE_MODULEDIR
  REGINA_CHECK_KDE_VERSION
else
  # There are some tests from AC_PATH_KDE that need to be run regardless.
  AC_CHECK_FUNCS([vsnprintf snprintf])

  # There are also some conditionals that must be defined.
  AM_CONDITIONAL(include_ARTS, false)
fi

REGINA_SHOULD_BUILD_INTERNAL(MPI)
if test "$should_build" = "yes"; then
  ACX_MPI([], [
    REGINA_WARN_MISSING([MPI development environment], [MPI-enabled utilities])
    REGINA_DO_NOT_COMPILE(MPI)
  ])
fi

###########################################################
#
# Miscellaneous other headers.
#
###########################################################

AC_HEADER_STDC
AC_CHECK_HEADERS([unistd.h])

###########################################################
#
# Verify that shared libraries are enabled if required.
#
###########################################################

REGINA_SHOULD_BUILD_INTERNAL(KDEUI)
if test "$should_build" = "yes"; then
  REGINA_LIBTOOL_SHLIBS_REQUIRED([the KDE user interface], [--disable-kdeui])
fi

REGINA_SHOULD_BUILD_INTERNAL(PYTHON)
if test "$should_build" = "yes"; then
  REGINA_LIBTOOL_SHLIBS_REQUIRED([the Python interface], [--disable-python])
fi

###########################################################
#
# Finalise include and library paths.
#
###########################################################

# Includes required by the calculation engine.
# AM_PATH_XML2 seems to have switched recently from using XML_CFLAGS to
# XML_CPPFLAGS; we'd better allow for either here.
ENGINE_EXTERNAL_INCLUDES="$XML_CFLAGS $XML_CPPFLAGS"
ENGINE_INCLUDES="-I\$(top_srcdir)/engine $ENGINE_EXTERNAL_INCLUDES"
AC_SUBST(ENGINE_EXTERNAL_INCLUDES)
AC_SUBST(ENGINE_INCLUDES)

# Libraries required by the calculation engine.
ENGINE_LIBS="$XML_LIBS $LIBPTHREAD"
AC_SUBST(ENGINE_LIBS)

# Additional libraries required by the test suite.
TESTSUITE_LIBS="$CPPUNIT_LIBS"
AC_SUBST(TESTSUITE_LIBS)

# The set of all Qt/KDE libraries required by the GUI.
GUI_KDE_LIBS="$LIB_KPARTS $LIB_KABC $LIB_KDEUI $LIB_KIO $LIB_KDECORE $LIB_QT -lktexteditor"
AC_SUBST(GUI_KDE_LIBS)

# Do we wish to statically link the calculation engine into final targets?
__static_final=no
LIB_ENGINE="\$(top_builddir)/engine/libregina-engine.la"
AC_ARG_ENABLE(static-final, AC_HELP_STRING([--enable-static-final],
    [statically link the calculation engine into final executables and modules [[default=no]]]), [
  if test "$enableval" != "no"; then
    __static_final=yes
    LIB_ENGINE="\$(top_builddir)/engine/libregina-engine-noinst.la"
  fi
])
AM_CONDITIONAL(STATIC_FINAL, test "$__static_final" = "yes")

# The actual calculation engine library with which to link; note that
# this depends on whether --enable-static-final is being used.
AC_SUBST(LIB_ENGINE)

###########################################################
#
# Create alternate sets of compiler flags.
#
###########################################################

# Make a set of compilation flags without debugging symbols.
CXXFLAGS_NODEBUG="`echo "$CXXFLAGS" | sed 's/-g[[0-9]]\? //'`"
AC_SUBST(CXXFLAGS_NODEBUG)

###########################################################
#
# Decide what should and should not be built.
#
###########################################################

REGINA_SHOULD_BUILD(ENGINE, engine, [the calculation engine library])
REGINA_SHOULD_BUILD(KDEUI, kdeui, [the KDE user interface])
REGINA_SHOULD_BUILD(PYTHON, python, [the Python interface])
REGINA_SHOULD_BUILD(UTILS, utils, [command-line utilities])
REGINA_SHOULD_BUILD(MPI, mpi, [MPI-enabled utilities])
REGINA_SHOULD_BUILD(TESTSUITE, testsuite, [the test suite])
REGINA_SHOULD_BUILD(DOCSENGINE, engine, [calculation engine docs])

AM_CONDITIONAL(COND_BUILD_PYTHON, test -n "$REGINA_BUILD_PYTHON")

###########################################################
#
# Prepare output files.
#
###########################################################

